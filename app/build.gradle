plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp' version "${kotlin_version}-1.0.11"
}

def props = new Properties()
File propsFile = file("../secrets.properties")
if (propsFile.isFile()) {
    propsFile.withInputStream { props.load(it) }
}

android {
    namespace 'com.asadullah.criticalnewsapp'
    compileSdk 33

    defaultConfig {
        applicationId "com.asadullah.criticalnewsapp"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "NEWS_API_SECRET_KEY", "\"${props.getProperty("NEWS_API_SECRET")}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        bbc {
            dimension "version"
            applicationIdSuffix ".bbc"
            buildConfigField "String", "SOURCE_ID", "\"bbc-news\""
            buildConfigField "String", "SOURCE_NAME", "\"BBC News\""
        }

        cbs {
            dimension "version"
            applicationIdSuffix ".cbs"
            buildConfigField "String", "SOURCE_ID", "\"cbs-news\""
            buildConfigField "String", "SOURCE_NAME", "\"CBS News\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_version"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'

    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation platform('androidx.compose:compose-bom:2022.10.00')

    // For Android Lifecycle aware flow collection in Compose
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"

    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'

    // Kotlin Activity
    implementation 'androidx.activity:activity-ktx:1.7.2'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'

    // Hilt
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    ksp("com.squareup.moshi:moshi-kotlin-codegen:1.14.0")
    implementation("com.squareup.moshi:moshi-kotlin:1.14.0")
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'

    // Coil
    implementation 'io.coil-kt:coil-compose:2.4.0'

    // Jode Time
    implementation 'joda-time:joda-time:2.12.5'

    // Destinations library
    def destinations_version = "1.8.42-beta"
    implementation "io.github.raamcosta.compose-destinations:core:$destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$destinations_version"

    // Datastore
    implementation 'androidx.datastore:datastore-core:1.0.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    // Biometric Manager
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"

    // System UI Controller
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.30.1"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}